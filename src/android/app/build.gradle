apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "org.citra.emu"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode(getBuildVersionCode())
        versionName "${getVersion()}"
    }

    buildTypes {
        release {
            // No signing config here â€” GitHub will use unsigned debug keystore
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            jniDebuggable true
        }
    }

    externalNativeBuild {
        cmake {
            path "../../../CMakeLists.txt"
            version '3.31.1'
        }
    }

    ndkVersion = '27.2.12479018'

    lint {
        abortOnError false
    }

    namespace 'org.citra.emu'

    defaultConfig {
        externalNativeBuild {
            cmake {
                arguments "-DENABLE_QT=0",
                        "-DENABLE_SDL2=0",
                        "-DENABLE_WEB_SERVICE=0",
                        "-DANDROID_ARM_NEON=true",
                        "-DENABLE_CUBEB=1",
                        "-DANDROID_STL=c++_static",
                        "-DCMAKE_BUILD_TYPE=Release",
                        "-DCMAKE_CXX_FLAGS_RELEASE=-Ofast"
                abiFilters "arm64-v8a"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.core:core-google-shortcuts:1.0.1"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
}

def getVersion() {
    def versionNumber = '0.0'
    try {
        versionNumber = 'git describe --always --long'.execute([], project.rootDir).text
                .trim()
                .replaceAll(/(-0)?-[^-]+$/, "")
    } catch (Exception e) {
        logger.error('Cannot find git, defaulting to dummy version number')
    }
    return versionNumber
}

def getBuildVersionCode() {
    try {
        def versionNumber = 'git rev-list --first-parent --count HEAD'.execute([], project.rootDir).text
                .trim()
        return Integer.valueOf(versionNumber)
    } catch (Exception e) {
        logger.error('Cannot find git, defaulting to dummy version number')
    }
    return 0
}
